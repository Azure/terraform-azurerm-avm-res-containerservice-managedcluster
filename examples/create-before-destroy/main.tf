terraform {
  required_version = ">= 1.9.0"
  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = ">= 4.0.0, < 5.0.0"
    }
    random = {
      source  = "hashicorp/random"
      version = "~> 3.5"
    }
  }
}

provider "azurerm" {
  features {
    resource_group {
      prevent_deletion_if_contains_resources = false
    }
  }
}

locals {
  locations = [
    "eastus",
    "eastus2",
    "westus2",
    "centralus",
    "westeurope",
    "northeurope",
    "southeastasia",
    "japaneast",
  ]
}

# This allows us to randomize the region for the resource group.
resource "random_integer" "region_index" {
  max = length(local.locations) - 1
  min = 0
}
## End of section to provide a random Azure region for the resource group

locals {
  location = local.locations[random_integer.region_index.result]
}

# This ensures we have unique CAF compliant names for our resources.
module "naming" {
  source  = "Azure/naming/azurerm"
  version = "~> 0.3"
}

# This is required for resource modules
resource "azurerm_resource_group" "this" {
  location = local.location
  name     = module.naming.resource_group.name_unique
}

data "azurerm_client_config" "current" {}

module "create_before_destroy" {
  source = "../.."

  default_node_pool = {
    name                         = "default"
    vm_size                      = "Standard_DS2_v2"
    auto_scaling_enabled         = true
    max_count                    = 4
    max_pods                     = 30
    min_count                    = 2
    only_critical_addons_enabled = true

    upgrade_settings = {
      max_surge = "10%"
    }
  }
  location            = azurerm_resource_group.this.location
  name                = module.naming.kubernetes_cluster.name_unique
  dns_prefix = "createexample"
  resource_group_name = azurerm_resource_group.this.name
  azure_active_directory_role_based_access_control = {
    azure_rbac_enabled = true
    tenant_id          = data.azurerm_client_config.current.tenant_id
  }
  create_nodepools_before_destroy = true
  managed_identities = {
    system_assigned = true
  }
  network_profile = {
    network_plugin = "kubenet"
  }
  node_pools = {
    unp1 = {
      name                 = "unp1"
      vm_size              = "Standard_DS2_v2"
      auto_scaling_enabled = true
      max_count            = 4
      max_pods             = 30
      min_count            = 2
      os_disk_size_gb      = 128
      upgrade_settings = {
        max_surge = "10%"
      }
    }
    unp2 = {
      name                 = "unp2"
      vm_size              = "Standard_DS2_v2"
      auto_scaling_enabled = true
      max_count            = 4
      max_pods             = 30
      min_count            = 2
      os_disk_size_gb      = 128
      upgrade_settings = {
        max_surge = "10%"
      }
    }
  }
}
